plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}

repositories {
    maven { url 'https://maven.shedaniel.me' }
    maven { url 'https://maven.wispforest.io/releases' }
    maven { url 'https://maven.terraformersmc.com/releases' }
    maven { url 'https://maven.kosmx.dev/' }
    maven { url 'https://api.modrinth.com/maven' }
    maven { url 'https://maven.ladysnake.org/releases' }
    maven { url 'https://maven.su5ed.dev/releases' }
    maven { url 'https://maven.jamieswhiteshirt.com/libs-release' }
    maven { url 'https://maven.fabricmc.net' }
    maven { url 'https://maven.parchmentmc.org' }
    maven { url 'https://dvs1.progwml6.com/files/maven/' }
    maven { url 'https://maven.blamejared.com/' }
    maven { url 'https://modmaven.dev' }

    exclusiveContent {
        forRepository {
            maven { url 'https://cursemaven.com' }
        }
        filter {
            includeGroup 'curse.maven'
        }
    }

    maven {
        url 'https://maven.jamieswhiteshirt.com/libs-release'
        content {
            includeGroup 'com.jamieswhiteshirt'
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"

    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modImplementation "io.wispforest:accessories-fabric:${accessories}"

    include modCompileOnly("dev.kosmx.player-anim:player-animation-lib-fabric:${player_anim}")

    modCompileOnly "maven.modrinth:better-combat:${bettercombat}-fabric"

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${rei}"

    modCompileOnlyApi "mezz.jei:jei-${minecraft_version}-fabric-api:${jei}"
    modCompileOnly "mezz.jei:jei-${minecraft_version}-fabric:${jei}"
    modRuntimeOnly "mezz.jei:jei-${minecraft_version}-fabric:${jei}"

    modApi "me.shedaniel.cloth:cloth-config:${cloth_config}"

    include modImplementation("com.jamieswhiteshirt:reach-entity-attributes:${project.reach_version}")

    modApi "com.terraformersmc:modmenu:${modmenu}"

    modImplementation "io.wispforest:owo-lib:${owo_version}"
    include "io.wispforest:owo-sentinel:${owo_version}"
    annotationProcessor "io.wispforest:owo-lib:${owo_version}"

    compileOnly project(":common")
    runtimeOnly project(":common")

    include modImplementation("maven.modrinth:streq:${streq}")
}

sourceSets {
    main {
        resources {
            srcDirs += ['src/generated/resources']
        }
    }
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
        datagen {
            inherit server
            name "Data Generation"
            ideConfigGenerated(true)
            runDir "run-datagen"

            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
            vmArg "-Dfabric-api.datagen.modid=${mod_id}"
            vmArg "-Dfabric-api.datagen.strict-validation=false"
            vmArg "--add-opens"
            vmArg "java.base/java.lang=ALL-UNNAMED"
        }
    }
}
